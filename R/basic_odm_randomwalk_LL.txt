model{
 # JAGS code for SPARTA model plus random walk prior
# on the year effect of the state model + intercept + halfcauchy hyperpriors

# State model
for (i in 1:nsite){ 
  for (t in 1:nyear){   
    z[i,t] ~ dbern(muZ[i,t]) 
    logit(muZ[i,t])<- a[t] + eta[i] 
  }}   

# Observation Model
for(j in 1:nvisit) {
  y[j] ~ dbern(Py[j])
  Py[j]<- z[site[j],year[j]]*p[j]
  logit(p[j]) <-  alpha.p[year[j]]+ beta.L*L[j] 
}


# State model priors
a[1] ~ dnorm(0, 0.001)
for(t in 2:nyear){
  a[t] ~ dnorm(a[t-1], tau.a)
}

tau.a <- 1/(sd.a * sd.a)
sd.a ~ dt(0, 1, 1)T(0,) 
  
for (i in 1:nsite) {
  eta[i] ~ dnorm(0, tau2)       
} 

tau2 <- 1/(sigma2 * sigma2) 
sigma2 ~ dt(0, 1, 1)T(0,) 
  
# Observation model priors 
for (t in 1:nyear) {
  alpha.p[t] ~ dnorm(mu.lp, tau.lp)            
}

mu.lp ~ dnorm(0, 0.01)
tau.lp <- 1 / (sd.lp * sd.lp)                 
sd.lp ~ dt(0, 1, 1)T(0,)  
  
beta.L ~ dnorm(0,0.01)
  
# Derived parameters
for (t in 1:nyear) {  
  psi.fs[t] <- sum(z[1:nsite, t])/nsite
} 

# overall trend in occpuancy
sumY <- sum(psi.fs[1:nyear])
for (t in 1:nyear) {
	sumxy[t] <- psi.fs[t]*t
}
sumXY <- sum(sumxy[1:nyear])
regres.psi <- (sumXY - ((sumX*sumY)/nyear))/(sumX2 - ((sumX*sumX)/nyear))

#calculate mean occupancy
mean.psi <- mean(psi.fs)

#occupancy - on probability scale
first.psi <- psi.fs[1]
last.psi <- psi.fs[nyear]

#get mean change in occupancy
mean.psi.change <-  (last.psi-first.psi)/first.psi

#log odds change in occupancy
odds.psi.change <- a[nyear] - a[1]

#calculate mean detection probability
mean.p <- mean(p) 

} 
